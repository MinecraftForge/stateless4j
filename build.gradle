plugins {
    id 'java'
    id 'maven'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'net.minecraftforge'
archivesBaseName = project.name.toLowerCase()
version = '2.7'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'

    testCompile 'junit:junit:4.11'
    testRuntime 'org.slf4j:slf4j-jdk14:1.7.7'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    from 'examples'
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories.mavenDeployer {
        dependsOn 'build'

        if (project.hasProperty('forgeMavenPass')) {
            repository(url: "http://files.minecraftforge.net/maven/manage/upload") {
                authentication(userName: "forge", password: project.getProperty('forgeMavenPass')) // the elvis operator. look it up.
            }
        } else {
            // local repo folder. Might wanna juset use  gradle install   if you wanans end it to maven-local
            repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName

            project {
                name = project.name
                packaging = 'jar'
                description = project.description
                url = project.url

                scm {
                    url 'https://github.com/MinecraftForge/stateless4j'
                    connection 'scm:git:https://github.com/MinecraftForge/stateless4j.git'
                    developerConnection 'scm:git:https://github.com/MinecraftForge/stateless4j.git'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/MinecraftForge/stateless4j/issues'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name 'Arnaud SYLVESTRE'
                        roles { role 'developer' }
                    }
                    developer {
                        name 'John Oxley'
                        roles { role 'developer' }
                    }
                }
            }
        }
    }
}
